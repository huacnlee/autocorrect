item = _{ SOI ~ line* ~ EOI }
line = _{ expr | newline }

expr = _{ 
  comment | html_tag | meta_info | block | inline | td_tag
}

block = ${ 
  meta_tags
  | list
  | codeblock
  | block_item
  | paragraph  
}
inline = ${ wikilinks | img | link | code | mark }
paragraph = { (string | inline)+ }

html_tag = @{ "<" ~ "/"? ~ (!(">") ~ ANY)* ~ ">"  }


newline = ${ "\n" | "\r" }
space = @{ " "* }
indent = @{ " "{4,} | "\t" }

codeblock = ${
  PUSH("```") ~ codeblock_lang ~ codeblock_code ~ POP
  | indent_code+
}
indent_code = @{
  indent ~ (!"\n" ~ ANY)* ~ newline
}
codeblock_lang = { ASCII_ALPHA* }
codeblock_code = { (!(PEEK) ~ ANY)* }
td_tag = @{ space ~ "|" ~ space }

block_prefix = @{
   ("######" | "#####" | "####" | "###" | "##" | "#" | ">") ~ " "*
}
block_item = { (block_prefix ~ (inline | string)+)  }

list = { list_item ~ (list_item | list_paragraph)*  }
list_item = ${ list_prefix ~ (inline | string)+ ~ newline+ }
list_paragraph = { indent ~ (inline | string)* ~ newline+ }
list_prefix = @{
  /*
    - Foo
    * Foo
    1. Foo
    [ ] Foo
    [x] Foo
  */
  (
    " "* ~ 
    (
      "*" 
      | "-" 
      | ASCII_DIGIT ~ "." 
      | "[" ~ (" " | "x" | "X") ~ "]"
    ) ~ " "*
  )
}

meta_info = ${ meta_wrap ~ newline ~ meta_pair* ~ meta_wrap ~ newline* }
meta_wrap = @{ "-"{3,} }
meta_pair = ${ meta_key ~ string ~ newline }
meta_key = @{ (!(":" | newline) ~ ANY)* ~ ":" ~ " "* }

// Ignore tags in Markdown
// tags: 美国, 中国
meta_tags = @{ meta_key ~ meta_tags_val+ ~ meta_tags_item }
meta_tags_val = @{ meta_tags_item ~ meta_tags_divider }
meta_tags_item = { (!(newline | ",")  ~ ANY)* }
meta_tags_divider = { " "* ~ "," ~ " "* }

image_prefix = @{ "!" }
img = ${ image_prefix ~ link }

link = ${ link_string ~ href }
link_string = { "[" ~ (!("]" | newline) ~ ANY)* ~ "]" }
href = { paren }

wikilinks = ${"[[" ~ (!("]]" | newline) ~ ANY)* ~ "]]" }

mark = ${ PUSH(open_mark) ~ (mark | mark_string) ~ close_mark }
open_mark = @{ "***" | "**" | "*" | "~~" | "`" | "\"" }
close_mark = @{ POP }
mark_string = { (!(PEEK | newline | inline) ~ ANY)* }

code = ${ PUSH(open_code) ~ inner_code ~ close_code }
open_code = @{ "`" }
close_code = @{ POP }
inner_code = @{ (!(newline | PEEK) ~ ANY)*  }

string = @{ (!(newline | inline) ~ ANY)+ }

comment = ${ open_comment ~ (!close_comment ~ ANY)* ~ close_comment }
open_comment = @{  "<!--" }
close_comment = @{ "-->" }

paren = { open_paren ~ paren* ~ close_paren | inner_paren }
inner_paren = { (!(newline | open_paren | close_paren) ~ ANY)+ } 
open_paren = { "(" }
close_paren = { ")" }